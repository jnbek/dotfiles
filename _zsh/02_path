home_subdirs=( src bin etc lib opt var tmp local perl5 tmp/vim/bak .zcompcache/$HOST );

for d in ${home_subdirs};
do 
    if [[ ! -e "$HOME/$d" ]];
    then
        echo "mkdir $HOME/$d: Sucessfully Created."; 
        mkdir -p $HOME/$d;
    fi;
done;

typeset -U common_paths

common_paths=(                       
    #${path} ${=$(command -p getconf PATH)//:/ } # what the system thinks PATH should be
    /bin /sbin /usr/bin /usr/sbin               # good places to look                  
    /usr/local/bin /usr/local/sbin              # FreeBSD                              
    /usr/libexec /usr/local/libexec             # FreeBSD
    /usr/X11R6/bin                              # X11                          
    /usr/X11/bin                                # X11/MacOSX
    /usr/pkg/bin /usr/pkg/sbin                  # NetBSD                               
    /usr/ucb                                    # Solaris - BSD                
    /usr/sfw/bin /usr/sfw/sbin                  # Solaris - sun free-ware              
    /usr/xpg4/bin /usr/xpg6/bin                 # Solaris - X/Open Portability Guide   
    /opt/local/bin /opt/local/sbin              # Solaris                                      
    /opt/SUNWspro/bin                           # Solaris                                      
    /usr/ccs/bin                                # Solaris - C Compilation System
    /usr/platform/${MACHTYPE}/sbin              # Solaris - hardware dependent          
    /var/qmail/bin                              # qmail
    /usr/games                                  # fun stuff
    /usr/games/bin                              # Gentoo Linux/FreeBSD                            
    /var/www/bin                                # Misc
    $HOME/bin 
    $HOME/wrk
    # Grrr Mac 
    /usr/local/git/bin
    $HOME/scala/bin
    $HOME/scala-sbt/bin
    # Go Language
    /usr/lib/go/site/bin
    /usr/local/go/bin
    /usr/local/lib/go/site/bin
    /opt/go/bin
    # Perl
    /usr/bin/core_perl
    /usr/bin/site_perl
    /usr/bin/vendor_perl
);           

unset PATH_tmp PATH
unsetopt NOMATCH                                                                        

for temp_path in ${common_paths};
do                                                                                      
    if [[ ${OSTYPE} == solaris* ]]; 
    then                                                      
        ## solaris may has some of these directories owned by "bin:bin" (uid 2)       
        ## observed on: SunOS 5.10 Generic_120011-14 sparc, core install              
        test -d "${temp_path}"(u0r^IWt,u2r^IWt,Ur^IWt) && PATH_tmp="${PATH_tmp}${temp_path}:"
    elif [[ ${OSTYPE} == *freebsd* && "$(/sbin/sysctl -n security.jail.jailed)" == 1 ]];
    then
        ## in a bsd jail, look for symlinks, not just directories
        test -e "${temp_path}"(-/u0r^IWt,-/Ur^IWt) && PATH_tmp="${PATH_tmp}${temp_path}:"
    else 
        test -d "${temp_path}"(u0r^IWt,Ur^IWt) && PATH_tmp="${PATH_tmp}${temp_path}:"        
        ## the next line shows how to include symlinks in the PATH                           
        #test -e "${temp_path}"(-/u0r^IWt,-/Ur^IWt) && PATH_tmp="${PATH_tmp}${temp_path}:"   
    fi;                                                                                            
done;

export PATH=${PATH_tmp/%:/}

# Brew futzes this up...
[[ -e /usr/local/bin/brew ]] && export PATH=${PATH}:/usr/local/bin

# Fix up the GOPATH stuff

if [[ -x "$(whence -p go)" ]];
then
    go_dir=$HOME/var/go/pkgs;
    if [[ ! -e "$go_dir" ]];
    then
        mkdir -p $go_dir;
        echo "$go_dir Successfully created";
    fi;
    export GOPATH=$go_dir;
    unset go_dir
fi;

# Fix Node/NPM Paths
if [[ -x "$(whence -p npm)" ]];
then
    npm_dir=$HOME/var/npm/pkgs;
    if [[ ! -e "$npm_dir" ]];
    then
        mkdir -p $npm_dir;
        echo "$npm_dir Successfully created";
    fi;
    export PATH=${PATH}:${npm_dir}/bin;
    unset npm_dir
fi;

unset common_paths temp_path PATH_tmp home_subdirs

# vim:set ft=zsh:
